if (!exists("dim")) dim=10
if (!exists("filename")) filename='out.dat'

set style function lines
set size 1.0, 1.0
set origin 0.0, 0.0
set multiplot
set size 0.5,0.5
set origin 0.0,0.5
set grid
#unset key

# Plot fvalue, sigma, kappa
set title "f-value, sigma, kappa"
set xlabel "function evaluations"
plot filename u 2:1 w lines title "f-value", filename u 2:3 w lines title "sigma", filename u 2:4 w lines title "kappa"

# Plot eigenvalues
set size 0.5,0.5
set origin 0.0,0.0
set title "Eigenvalues"
set logscale y
unset key
plot for [i=4:(dim+4)] filename u 2:i w lines
unset logscale y

# Plot xmean
set size 0.5,0.5
set origin 0.5,0.5
set title "X mean"
plot for [i=(2*dim+5):(3*dim+5-1)] filename u 2:i w lines

# Plot std deviation
set origin 0.5,0.
set logscale y
set title "Std deviation"
plot for [i=dim+5:(2*dim+5-1)] filename u 2:i w lines

pause -1 "Hit return to continue"

#unset multiplot